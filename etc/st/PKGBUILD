# Maintainer: Ryota Kota <ryota.kota@member.fsf.org>

pkgname=st
pkgver=0.8.2
pkgrel=2
pkgdesc='A simple virtual terminal emulator for X.'
arch=('i686' 'x86_64' 'armv7h')
license=('MIT')
depends=('libxft' 'libxext' 'xorg-fonts-misc' 'pkg-config')
makedepends=('ncurses')
url="http://st.suckless.org"
source=(http://dl.suckless.org/st/$pkgname-$pkgver.tar.gz
        st.desktop
        st.svg
        config.h)
sha256sums=('aeb74e10aa11ed364e1bcc635a81a523119093e63befd2f231f8b0705b15bf35'
            '2cd8f3e89587a730cd2665cfb66d8eda9f37709a4118fb9f41919ef4fd5706ed'
            'a17f8c3c5857d5117b53f96bfabb537a55319665ce5cf8f8cf0df3574343b8f5'
            'SKIP')

prepare() {
  cp $srcdir/config.h $srcdir/$pkgname-$pkgver/config.h
}

build() {
  cd $srcdir/$pkgname-$pkgver
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
  # install desktop application
  install -Dm644 "${srcdir}/st.desktop" "${pkgdir}/usr/share/applications/st.desktop"
  install -Dm644 "${srcdir}/st.svg" "${pkgdir}/usr/share/st/st.svg"
  # remove to avoid conflict with ncurses
  rm "${pkgdir}/usr/share/terminfo/s/st" "${pkgdir}/usr/share/terminfo/s/st-256color"
}
